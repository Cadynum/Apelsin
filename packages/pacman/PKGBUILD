# Maintainer: Christoffer Ã–jeling <christoffer@ojeling.net>
pkgname=apelsin
pkgver=1.2.3
pkgrel=2
pkgdesc="Server and community browser for the game Tremulous"
url="http://ojeling.net/apelsin"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('ghc' 
    'haskell-http>=4000'
    'haskell-glib'
    'haskell-gtk'
    'haskell-mtl>=2.0'
    'haskell-network>=2.3'
    'cabal-install'
)
depends=('gmp' 'gtk2' 'hicolor-icon-theme')

optdepends=('tremulous: Launch Tremulous 1.1 games'
            'tremulous-gpp: Launch Tremulous GPP games'
            'unvanquished: Launch Unvanquished games')


options=('strip')
install="${pkgname}.install"

source=("https://hackage.haskell.org/package/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")

_uglydeps=('hashable-1.2.2.0'
           'scientific-0.3.2.1'
           'attoparsec-0.11.3.4'
           'xdg-basedir-0.2.2'
           'tremulous-query-1.0.7'
)
for dep in "${_uglydeps[@]}"
do
    source+=("https://hackage.haskell.org/package/$dep/$dep.tar.gz")
done

md5sums=('c41e93d15cff8f3a2f943b135d5916f4'
         '74981d48572ef7a97fd70df54be29aae'
         'dedca94b466b79310425c4b3bbda0c7b'
         '95917e525a6d2a6a81be51cd816e4ca1'
         '0122cf87ba58b0be74cd341d5afa0789'
         'ce7071f8a730d1095979cb6a79e37e45')

build() {
    cd ${srcdir}
    mkdir -p sandbox
    cd sandbox
    cabal sandbox init --sandbox .

    for dep in "${_uglydeps[@]}"
        do
            cd "../$dep"
            cabal sandbox init --sandbox ../box
            cabal configure
            cabal build 
            cabal install
        done

    cd ${srcdir}/${pkgname}-${pkgver}
    cabal sandbox init --sandbox ../box
    # subdir is somewhat of a hack to set the path for the icons correctly
    cabal configure --prefix=/usr --datasubdir='' -O
    cabal build
}


package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    install -D -m755 "dist/build/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    cp -a "share" "${pkgdir}/usr"
}
